icon_sizes = ['48', '64', '128']

foreach i : icon_sizes
    install_data(
        join_paths('icons', i, meson.project_name() + '.svg'),
        install_dir: join_paths(get_option('datadir'), 'icons', 'hicolor', i + 'x' + i, 'apps')
    )
    install_data(
        join_paths('icons', i, meson.project_name() + '.svg'),
        install_dir: join_paths(get_option('datadir'), 'icons', 'hicolor', i + 'x' + i + '@2', 'apps')
    )
endforeach

install_data(
    meson.project_name() + '.gschema.xml',
    install_dir: join_paths(get_option('prefix'), get_option('datadir'), 'glib-2.0', 'schemas')
)

i18n.merge_file(
    input: meson.project_name() + '.desktop.in',
    output: meson.project_name() + '.desktop',
    po_dir: join_paths(meson.source_root(), 'po', 'extra'),
    type: 'desktop',
    install: true,
    install_dir: join_paths(get_option('datadir'), 'applications')
)

i18n.merge_file(
    input: meson.project_name() + '.appdata.xml.in',
    output: meson.project_name() + '.appdata.xml',
    po_dir: join_paths(meson.source_root(), 'po', 'extra'),
    install: true,
    install_dir: join_paths(get_option('datadir'), 'metainfo')
)

figlet_files = [
    'rectangles.flf',
    'isometric1.flf',
    'isometric2.flf',
    'isometric3.flf',
    'isometric4.flf',
    'larry3d.flf',
    'ogre.flf',
    'graffiti.flf',
    'drpepper.flf',
    'double.flf',
    'doom.flf',
    'rounded.flf',
    'speed.flf',
]
foreach f : figlet_files
    install_data(
        join_paths('figlet-fonts', f),
        install_dir: join_paths(get_option('datadir'), 'figlet')
    )
endforeach
